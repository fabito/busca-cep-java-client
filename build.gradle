buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'com.jfrog.bintray'

version =  '1.8'
group = 'com.github.fabito'

def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
repositories {
    repositories {
        jcenter()
    }
    maven {
        url localMavenRepo
    }    
}

jar {
	baseName = 'busca-cep-java-client'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}


Properties properties = new Properties()
try {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch(all) {
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    dryRun = false 
    publish = true    
    pkg {
        repo = 'maven'
        name = 'busca-cep-java-client'
        userOrg = 'fabito'
        licenses = ['Apache-2.0','MIT']
        vcsUrl = 'https://github.com/fabito/busca-cep-java-client.git'
        issueTrackerUrl = 'https://github.com/fabito/busca-cep-java-client/issues'
        labels = ['busca', 'cep', 'java', 'http', 'client', 'busca-cep']
    }
}
